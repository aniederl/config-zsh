# Filename:         /etc/zsh/functions/prompt_gentoo_setup
# Purpose:          gentoo prompt theme
# Author:           (c) Andreas Niederl <rico32@gmx.net>
# License:          This file is licensed under the GPL v2.
# Additional Info:  This file is based on the file prompt_gentoo_setup shipped
#                   with the Gentoo package app-shells/zsh
##############################################################################

prompt_gentoo_help () {
  cat <<'EOF'
This prompt is color-scheme-able.  You can invoke it thus:

  prompt gentoo [<promptcolor> [<usercolor> [<rootcolor> [<userstatuscolor> [ <rootstatuscolor>]]]]]

EOF
}

prompt_gentoo_precmd () {
  setopt noxtrace noksharrays localoptions
  local exitstatus=$?
  local git_dir git_ref

  psvar=()
  [[ $exitstatus -ge 128     ]] && psvar[1]="SIG$signals[$exitstatus-127]" || psvar[1]=""

  #[[ -o interactive ]] && jobs -l

  vcs_info
  #[[ -n $vcs_info_msg_0_     ]] && psvar[2]="$vcs_info_msg_0_"             || psvar[2]=""
  #[[ -n $VCS_INFO_message_0_ ]] && psvar[2]="$VCS_INFO_message_0_"         || psvar[2]=""
}

prompt_gentoo_setup () {
  autoload -Uz vcs_info
  autoload colors zsh/terminfo
  if [[ "$terminfo[colors]" -ge 8 ]]; then
    colors
  fi

  prompt_gentoo_prompt=${1:-'blue'}
  prompt_gentoo_user=${2:-'green'}
  prompt_gentoo_root=${3:-'red'}
  prompt_gentoo_user_status=${4:-'red'}
  prompt_gentoo_root_status=${5:-'cyan'}

  if [ "$USER" = 'root' ]
  then
    base_prompt="%{$fg_bold[$prompt_gentoo_root]%}%m%{$reset_color%} "
    status_prompt="%(?..%{$fg_bold[$prompt_gentoo_root_status]%}[%?%1v]%{$reset_color%} )"
  else
    base_prompt="%{$fg_bold[$prompt_gentoo_user]%}%n@%m%{$reset_color%} "
     status_prompt="%(?..%{$fg_bold[$prompt_gentoo_user_status]%}[%?%1v]%{$reset_color%} )"
  fi
  post_prompt="%{$reset_color%}"
  #vcs_prompt="%(2V.%2v.)"

  local color="%{*}"
  base_prompt_no_color="${(S)base_prompt//${~color}/}"
  post_prompt_no_color="${(S)post_prompt//${~color}/}"

  setopt noxtrace localoptions
  local base_prompt_expanded_no_color base_prompt_etc
  local prompt_length space_left

  base_prompt_expanded_no_color=$(print -P "$base_prompt_no_color")
  base_prompt_etc=$(print -P "$status_prompt$base_prompt%(4~|...|)%3~")
  prompt_length=${#base_prompt_etc}
  path_prompt="%{$fg_bold[$prompt_gentoo_prompt]%}%1~%{$reset_color%}"
  prompt_separator="%{$fg_bold[$prompt_gentoo_prompt]%}"

  #PS1="$status_prompt$base_prompt$path_prompt \$vcs_info_msg_0_$prompt_separator%# $post_prompt"
  PS1="$status_prompt$base_prompt$path_prompt \$VCS_INFO_message_0_$prompt_separator%# $post_prompt"
  PS2="$status_prompt$base_prompt$path_prompt $prompt_separator%_> $post_prompt"
  PS3="$status_prompt$base_prompt$path_prompt $prompt_separator?# $post_prompt"

  functions[precmd]="prompt_gentoo_precmd
      ${functions[precmd]//prompt_*_precmd}"
  functions[preexec]="${functions[preexec]//prompt_*_preexec}"
}

prompt_gentoo_setup "$@"

# vim: ft=zsh
